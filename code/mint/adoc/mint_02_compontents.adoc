= Mint Lang Explorations
:source-highlighter: prettify
:source-language: mint
Bill Tihen (Mint 0.7.1)
:sectnums:
:toc:
:toclevels: 4
:toc-title: Contents

:description: Exploring Mint's Features
:keywords: Mint Language
:imagesdir: ./images

**link:index.html[Mint Page]**

== Components - the main building blocks in Mint

Components output html and they: 
* can be connected to *stores* (Stores are global containers of application specific data.)
* can subscribe to *providers* (a source of asynchronous events). These are: 
** Tick Provider - https://github.com/mint-lang/mint/blob/master/core/source/Provider/Tick.mint
** AnimationFrame (archived) - https://github.com/mint-lang/mint-core/blob/master/source/Provider/AnimationFrame.mint
** Mouse - https://github.com/mint-lang/mint/blob/master/core/source/Provider/Mouse.mint
** Scroll (archived) - https://github.com/mint-lang/mint-core/blob/master/source/Provider/Scroll.mint
* can have *styles* to defined HTML elements inside them
* can define *properties* (Property values are passed to the component when it is rendered). These properties can have types and default values 

=== Intitialize a project:

```bash
$ mint-lang init count_component
$ cd count_component 
$ mint-lang start
```

=== Counter Component

We will control our counter values and save the state centrally (this Store resets with every page reload / new connection)

.code/mint/src/counter/source/CounterStore.mint
[source,linenums]
----
/* CounterStore.mint */

store Counter.Store {
  
}
----

* **Stores** are global containers of application specific data. They are defined with the store keyword -- see: https://www.mint-lang.com/guide/reference/stores
* **States** - Stores (global public) & Components can have internal (private) states -- see: https://www.mint-lang.com/guide/reference/components/internal-state
* **Promises** - Promises represents asynchronous tasks that might fail -- see: https://www.mint-lang.com/guide/reference/built-in-types

=== Counter Component

.code/mint/src/counter_component/source/Counter.mint
[source,linenums]
----
/* Counter.mint */

component Counter {

  style counter {
    display: flex;
  }

  style counterButton {
    color: white;
    border: 0;
    margin: 0 7px 0;
    font-size: 18px;
    font-weight: bold;
  }

  style decrementButton {
    background: red;
  }

  style incrementButton {
    background: lime;
  }

  style counterText {
    font-size: 36px;
    margin: 0 7px 0;
  }

  state counter : Number = 0

  fun increment : Promise(Never, Void) {
    next { counter = counter + 1 }
  }

  fun decrement : Promise(Never, Void) {
    next { counter = counter - 1 }
  }

  fun countHtml (count : Number) : Html {
    <div::counterText>
      <{ "Count is: #{count}" }>
    </div>
  }

  fun render : Html {
    <div::counter>
      <button::counterButton::decrementButton
        onClick={decrement}>
        "-"
      </button>

      <{ countHtml(counter) }>

      <button::counterButton::incrementButton
        onClick={increment}>
        "+"
      </button>
    </div>
  }
}
----

=== Add our Counter Component to Main

we will replace `<{ "Hello Mint!" }>` with `<Counter/>`

so Main.mint now looks like:

.code/mint/src/counter_component/source/Main.mint
[source,linenums]
----
component Main {
  style main {
    font-family: sans;

    justify-content: center;
    align-items: center;
    display: flex;
    height: 100vh;
    width: 100vw;
  }

  fun render : Html {
    <div::main>
      <Counter/>
    </div>
  }
}
----

open `http://localhost:3000`

be sure you see: `Count is:` with a `+` and `-` buttons

**link:index.html[Mint Page]**