

Szikszai Guszt√°v @gdotdesign 16:06
you probably need to do something like this:
component Navbar {
  property children : Array(Html) = []

  state active : Bool = false

  fun handleClick (event : Html.Event) : Promise(Never, Void) {
    next { active = !active }
  }

  get burgerClass : String {
    if (active) {
      "navbar-burger is-active"
    } else {
      "navbar-burger"
    }
  }

  get menuClass : String {
    if (active) {
      "navbar-menu is-active"
    } else {
      "navbar-menu"
    }
  }

  fun render : Html {
    <>
      <a
        role="button"
        onClick={handleClick}
        class={burgerClass}
        data-target="navMenu"
        aria-label="menu"
        aria-expanded="false">

        <span aria-hidden="true"/>
        <span aria-hidden="true"/>
        <span aria-hidden="true"/>

      </a>

      <div
        class={menuClass}
        id="navMenu">

        <{ children }>

      </div>
    </>
  }
}

component Main {
  fun render : Html {
    <>
      <Navbar>
        "NAVBAR 1"
      </Navbar>

      <Navbar>
        "NAVBAR 2"
      </Navbar>
    </>
  }
}

